plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation project(':criteria')
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.boot:spring-boot-testcontainers")
    testImplementation("org.testcontainers:postgresql:1.21.1")
    testImplementation("org.testcontainers:junit-jupiter:1.21.1")
    testImplementation("org.postgresql:postgresql")
}

test {
    useJUnitPlatform()
}

bootJar {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.willypol78'
            artifactId = 'criteria-to-spring-jpa'
            from components.java

            pom {
                name = 'Criteria to Spring JPA'
                description = 'Transformaci√≥n de criterios a Spring Data JPA Specifications'
                url = 'https://github.com/willypol78/criteria'
                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'willypol78'
                        name = 'Guillermo Mir'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/willypol78/criteria.git'
                    developerConnection = 'scm:git:ssh://github.com/willypol78/criteria.git'
                    url = 'https://github.com/willypol78/criteria'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}