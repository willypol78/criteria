plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation project(':criteria')
    implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.18.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.1'

    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.willypol78'
            artifactId = 'criteria-from-http-servlet-request'
            from components.java

            pom {
                name = 'Criteria from HTTP Servlet Request'
                description = 'Librer√≠a para extraer criterios desde HttpServletRequest'
                url = 'https://github.com/willypol78/criteria'
                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'willypol78'
                        name = 'Guillermo Mir'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/willypol78/criteria.git'
                    developerConnection = 'scm:git:ssh://github.com/willypol78/criteria.git'
                    url = 'https://github.com/willypol78/criteria'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}