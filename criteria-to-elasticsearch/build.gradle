plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation project(':criteria')
    implementation 'co.elastic.clients:elasticsearch-java:8.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.1'

    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.willypol78'
            artifactId = 'criteria-to-elasticsearch'
            from components.java

            pom {
                name = 'Criteria to Elasticsearch'
                description = 'Transformaci√≥n de criterios a consultas Elasticsearch'
                url = 'https://github.com/willypol78/criteria'
                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'willypol78'
                        name = 'Guillermo Mir'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/willypol78/criteria.git'
                    developerConnection = 'scm:git:ssh://github.com/willypol78/criteria.git'
                    url = 'https://github.com/willypol78/criteria'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
